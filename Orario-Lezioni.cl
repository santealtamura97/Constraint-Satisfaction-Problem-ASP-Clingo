docente(profPresentazione; nonDefinito; muzzetto; pozzato; gena; tomatis; micalizio; terranova; mazzei; giordani; zanchetta; vargiu; boniolo; damiano; suppini; valle; ghidelli; gabardi; santangelo; taddeo; gribaudo; schifanella; lombardo; travosino;).


corso(blocco_libero).
corso(presentazione).
corso(project_management).
corso(fondamenti_di_ict_e_Paradigmi_di_programmazione).
corso(linguaggi_di_markup).
corso(la_gestione_della_qualita).
corso(ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web).
corso(progettazione_grafica_e_design_di_interfacce).
corso(progettazione_di_basi_di_dati).
corso(strumenti_e_metodi_di_interazione_nei_social_media).
corso(acquisizione_ed_elaborazione_di_immagini_statiche_grafica).
corso(accessibilita_e_usabilita_nella_progettazione_multimediale).
corso(marketing_digitale).
corso(elementi_di_fotografia_digitale).
corso(risorse_digitali_per_il_progetto_collaborazione_e_documentazione).
corso(tecnologie_server_side_per_il_web).
corso(tecniche_e_strumenti_di_marketing_digitale).
corso(introduzione_al_social_media_management).
corso(acquisizione_ed_elaborazione_del_suono).
corso(acquisizione_ed_elaborazione_di_sequenze_di_immagini_digitali).
corso(comunicazione_pubblicitaria_e_comunicazione_pubblica).
corso(semiologia_e_multimedialita).
corso(crossmedia_articolazione_delle_scritture_multimediali).
corso(grafica_3d).
corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i).
corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_ii).
corso(la_gestione_delle_risorse_umane).
corso(i_vincoli_giuridici_del_progetto_diritto_dei_media).

insegna(docente(profPresentazione),corso(presentazione)).
insegna(docente(nonDefinito),corso(blocco_libero)).
insegna(docente(muzzetto),corso(project_management)).
insegna(docente(pozzato),corso(fondamenti_di_ict_e_Paradigmi_di_programmazione)).
insegna(docente(gena),corso(linguaggi_di_markup)).
insegna(docente(tomatis),corso(la_gestione_della_qualita)).
insegna(docente(micalizio),corso(ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web)).
insegna(docente(terranova),corso(progettazione_grafica_e_design_di_interfacce)).
insegna(docente(mazzei),corso(progettazione_di_basi_di_dati)).
insegna(docente(giordani),corso(strumenti_e_metodi_di_interazione_nei_social_media)).
insegna(docente(zanchetta),corso(acquisizione_ed_elaborazione_di_immagini_statiche_grafica)).
insegna(docente(gena),corso(accessibilita_e_usabilita_nella_progettazione_multimediale)).
insegna(docente(muzzetto),corso(marketing_digitale)).
insegna(docente(vargiu),corso(elementi_di_fotografia_digitale)).
insegna(docente(boniolo),corso(risorse_digitali_per_il_progetto_collaborazione_e_documentazione)).
insegna(docente(damiano),corso(tecnologie_server_side_per_il_web)).
insegna(docente(zanchetta),corso(tecniche_e_strumenti_di_marketing_digitale)).
insegna(docente(suppini),corso(introduzione_al_social_media_management)).
insegna(docente(valle),corso(acquisizione_ed_elaborazione_del_suono)).
insegna(docente(ghidelli),corso(acquisizione_ed_elaborazione_di_sequenze_di_immagini_digitali)).
insegna(docente(gabardi),corso(comunicazione_pubblicitaria_e_comunicazione_pubblica)).
insegna(docente(santangelo),corso(semiologia_e_multimedialita)).
insegna(docente(taddeo),corso(crossmedia_articolazione_delle_scritture_multimediali)).
insegna(docente(gribaudo),corso(grafica_3d)).
insegna(docente(pozzato),corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i)).
insegna(docente(schifanella),corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_ii)).
insegna(docente(lombardo),corso(la_gestione_delle_risorse_umane)).
insegna(docente(travostino),corso(i_vincoli_giuridici_del_progetto_diritto_dei_media)).
nOreCorso(14,corso(project_management)).
nOreCorso(14,corso(fondamenti_di_ict_e_Paradigmi_di_programmazione)).
nOreCorso(20,corso(linguaggi_di_markup)).
nOreCorso(10,corso(la_gestione_della_qualita)).
nOreCorso(20,corso(ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web)).
nOreCorso(10,corso(progettazione_grafica_e_design_di_interfacce)).
nOreCorso(20,corso(progettazione_di_basi_di_dati)).
nOreCorso(14,corso(strumenti_e_metodi_di_interazione_nei_social_media)).
nOreCorso(14,corso(acquisizione_ed_elaborazione_di_immagini_statiche_grafica)).
nOreCorso(14,corso(accessibilita_e_usabilita_nella_progettazione_multimediale)).
nOreCorso(10,corso(marketing_digitale)).
nOreCorso(10,corso(elementi_di_fotografia_digitale)).
nOreCorso(10,corso(risorse_digitali_per_il_progetto_collaborazione_e_documentazione)).
nOreCorso(20,corso(tecnologie_server_side_per_il_web)).
nOreCorso(10,corso(tecniche_e_strumenti_di_marketing_digitale)).
nOreCorso(14,corso(introduzione_al_social_media_management)).
nOreCorso(10,corso(acquisizione_ed_elaborazione_del_suono)).
nOreCorso(20,corso(acquisizione_ed_elaborazione_di_sequenze_di_immagini_digitali)).
nOreCorso(14,corso(comunicazione_pubblicitaria_e_comunicazione_pubblica)).
nOreCorso(10,corso(semiologia_e_multimedialita)).
nOreCorso(20,corso(crossmedia_articolazione_delle_scritture_multimediali)).
nOreCorso(20,corso(grafica_3d)).
nOreCorso(10,corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i)).
nOreCorso(10,corso(progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_ii)).
nOreCorso(10,corso(la_gestione_delle_risorse_umane)).
nOreCorso(10,corso(i_vincoli_giuridici_del_progetto_diritto_dei_media)).
nOreCorso(4, corso(blocco_libero)).
nOreCorso(2, corso(presentazione)).



% creato per poter ordinare in base ai giorni
giorNum(1,lunedi;2,martedi;3,mercoledi;4,giovedi;5,venerdi;6,sabato).
orario(9,10; 10,11; 11,12; 12,13; 14,15; 15,16; 16,17; 17,18).
giorno(lunedi;martedi;mercoledi;giovedi;venerdi;sabato).
giornoFeriale(sabato).
settimana(1..24).

settimanaFull(7).
settimanaFull(16).

8 {ha_ore(Giorno, Ora):orario(Ora,_)} 8:- giorno(Giorno),not giornoFeriale(Giorno).
4 {ha_ore(Giorno,Ora):orario(Ora,_)} 5:- giornoFeriale(Giorno).
24 {settimane(S,venerdi):settimana(S)} 24.
24 {settimane(S,sabato):settimana(S)} 24.
6 {settimane(Settimana,Giorno): giorno(Giorno)} 6:- settimanaFull(Settimana).

% con la regola seguente si sta producendo un calendario imponendo che un Corso possa prevedere al più 4 ore al giorno
0 {slotLezione(Doc, Corso, Ora, Giorno, Settimana): orario(Ora,_)} 4:- insegna(docente(Doc), corso(Corso)), settimane(Settimana, Giorno), ha_ore(Giorno, Ora).

%si sta imponendo il controllo sulle ore di lezione del sabato(4 o 5 ore) usufruendo della regola ha_ore precedentemente dichiarata
:- slotLezione(Doc, Corso, Ora, Giorno, Settimana), not ha_ore(Giorno, Ora).

% Non ci possono essere più insegnamenti in una stessa ora di uno stesso giorno
:- slotLezione(_, Ins1, Ora, Giorno, Settimana), slotLezione(_, Ins2, Ora, Giorno, Settimana),
    Ins1 != Ins2.

% Imposizione del numero massimo di ore totale per Corso/presentazione/recupero
lezione(Corso, Ora, Giorno, Settimana):- slotLezione(_, Corso, Ora, Giorno, Settimana).


%Ogni giorno non feriale ha 8 ore di lezione. ogni giorno feriale ha non meno di 4 ore di lezione
oreGiorno(Giorno, Settimana, N):- N = #count{Ora:slotLezione(_, _, Ora, Giorno, Settimana)}, 
                                                            settimane(Settimana, Giorno).
:- oreGiorno(Giorno, Settimana, N), giornoFeriale(Giorno), N<4.
:- oreGiorno(Giorno, Settimana, N), giorno(Giorno), not giornoFeriale(Giorno), N<8.


%un Corso può avere minimo 2 e massimo 4 ore in uno stesso giorno. Quindi le soluzioni che prevedono una sola ora di lezione in uno stesso giorno, sono escluse


% a ciascun insegnamento vengono assegnate minimo 2 e massimo 4 ore nello stesso giorno
% il massimo di 4 ore è assicurato dal vincolo precedente
conteggioOreGiornaliere(Conteggio, Corso, Giorno, Settimana) :-
    Conteggio = #count{ Ora : slotLezione(_, Corso, Ora, Giorno, Settimana)}, slotLezione(_, Corso, _, Giorno, Settimana), corso(Corso). 
:- conteggioOreGiornaliere(Conteggio, Corso, Giorno, Settimana), Conteggio==1.

% permette di assegnare un numero di ore pari a quelle prestabilite per ogni materia
% lo stesso docente non può svolgere più di 4 ore di lezione in un giorno
conteggioOreProfessore(Conteggio, Docente, Giorno, Settimana) :-
    Conteggio = #count{ Ora : slotLezione(Docente, _, Ora, Giorno, Settimana)}, slotLezione(Docente, _, _, Giorno, Settimana), docente(Docente). 
:- conteggioOreProfessore(Conteggio, Docente, Giorno, Settimana), Conteggio>4.



%il primo giorno di lezione(il venerdì della settimana 1) prevede che, nelle prime due ore, vi sia la presentazione al master
:- slotLezione(Doc,presentazione, Ora, Giorno, Settimana), Ora > 10.                %esclusione delle soluzioni con presentazione prevista in ore successive alla seconda
:- slotLezione(Doc,presentazione, Ora, Giorno, Settimana), Giorno != venerdi.      %esclusione delle soluzioni con presentazione prevista in giorni diversi da venerdi
:- slotLezione(Doc,presentazione, Ora, Giorno, Settimana), Settimana != 1.         %esclusione delle soluzioni con presentazione non privista nella prima settimana

%sono previsti due blocchi da 2 ore ciascuno per l eventuale recupero di lezioni; verranno escluse tutte le soluzioni dove non sono previsti blocchi di 2 ore contigue
:- slotLezione(Doc, blocco_libero, Ora1, Giorno, Settimana), slotLezione(Doc, blocco_libero, Ora2, Giorno, Settimana), Ora1 != Ora2, |Ora1 - Ora2| != 1.



conteggioOreTotali(Corso, Conteggio) :-
    Conteggio = #count{ Settimana, Giorno, Ora : slotLezione(_, Corso, Ora, Giorno, Settimana)}, corso(Corso).
:- corso(Corso), nOreCorso(TotOre, corso(Corso)), conteggioOreTotali(Corso, N), N != TotOre.


lezione(Giorno, Inizio, Fine, Corso, Settimana, Docente):- slotLezione(Docente, Corso, Inizio, Giorno, Settimana), orario(Inizio,Fine).

% Permette di individuare lultima lezione di ogni corso.
primaSettimana(Giorno,Inizio,MinSettimana,Corso):- #min {Settimana:lezione(_, _, _, Corso, Settimana, _)} = MinSettimana, lezione(GG, Inizio,_,Corso,MinSettimana,_),giorNum(Giorno,GG).
primoGiorno(MinGiorno,Inizio,Settimana,Corso):- #min {Giorno:primaSettimana(Giorno,_, _, Corso)} = MinGiorno, primaSettimana(MinGiorno,Inizio, Settimana, Corso).
primaLezione(Giorno,MinInizio,Settimana,Corso):- #min {Inizio:primoGiorno(_,Inizio, _, Corso)} = MinInizio, primoGiorno(Giorno,MinInizio, Settimana, Corso).

ultimaSettimana(Giorno,Inizio,MinSettimana,Corso):- #max {Settimana:lezione(_, _, _, Corso, Settimana, _)} = MinSettimana, lezione(GG, Inizio,_,Corso,MinSettimana,_),giorNum(Giorno,GG).
ultimoGiorno(MinGiorno,Inizio,Settimana,Corso):- #max {Giorno:ultimaSettimana(Giorno,_, _, Corso)} = MinGiorno, ultimaSettimana(MinGiorno,Inizio, Settimana, Corso).
ultimaLezione(Giorno,MinInizio,Settimana,Corso):- #max {Inizio:ultimoGiorno(_,Inizio, _, Corso)} = MinInizio, ultimoGiorno(Giorno,MinInizio, Settimana, Corso).

% Permette di individuare il secondo giorno di lezione di ogni corso.
lezi(Giorno, Inizio,Corso, Settimana):-  giorNum(Ng,Giorno),lezione(Giorno, Inizio, _, Corso, Settimana, _), not primaLezione(Ng,_,Settimana,Corso).
settimanaSecondaLezione(Giorno,Inizio,MinSettimana,Corso):- #min {Settimana:lezi(_, _, Corso, Settimana)} = MinSettimana, lezi(GG, Inizio,Corso,MinSettimana),giorNum(Giorno,GG).
secondoGiornoLezione(MinGiorno,Inizio,Settimana,Corso):- #min {Giorno:settimanaSecondaLezione(Giorno,_, _, Corso)} = MinGiorno, settimanaSecondaLezione(MinGiorno,Inizio, Settimana, Corso).


% l’Corso “projectManagement” deve concludersi non oltre la prima settimana full-time
:- ultimaLezione(_, _,Settimana, project_management), Settimana > 7.

% la prima lezione dell’Corso “Accessibilità e usabilità nella progettazione multimediale” deve 
% essere collocata prima che siano terminate le lezioni dell’Corso “Linguaggi di markup”
:- ultimaLezione(_, _, Settimana, linguaggi_di_markup), primaLezione(_, _,Settimana2, accessibilita_e_usabilita_nella_progettazione_multimediale), Settimana2>Settimana.

% si definiscono le coppie di corsi che prevedono la propedeuticità, il primo corso deve terminare prima
% della prima lezione del secondo corso specificato.
propedeuticita(fondamenti_di_ict_e_Paradigmi_di_programmazione,ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web;ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web,progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i;linguaggi_di_markup,ambienti_di_sviluppo_e_linguaggi_client_side_per_il_web;marketing_digitale,tecniche_e_strumenti_di_marketing_digitale;project_management,strumenti_e_metodi_di_interazione_nei_social_media;project_management,progettazione_grafica_e_design_di_interfacce;elementi_di_fotografia_digitale,acquisizione_ed_elaborazione_di_sequenze_di_immagini_digitali;project_management,marketing_digitale;progettazione_di_basi_di_dati,tecnologie_server_side_per_il_web;acquisizione_ed_elaborazione_di_immagini_statiche_grafica,elementi_di_fotografia_digitale;acquisizione_ed_elaborazione_di_immagini_statiche_grafica,grafica_3d;progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i,progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_ii).
% Qui avviene leffettiva eliminazione di tutti i modelli che non rispettano le propedeuticità.
:- propedeuticita(M1,M2), ultimaLezione(_, _, Settimana1, M1), primaLezione(_, _, Settimana2, M2), Settimana2<Settimana1.
:- propedeuticita(M1,M2), ultimaLezione(Giorno1, _, Settimana, M1), primaLezione(Giorno2, _, Settimana, M2),  Giorno2<=Giorno1.

% la distanza tra la prima e l’ultima lezione di ciascun Corso non deve superare le 6 settimane
:- primaLezione(_, _, Settimana1, M), ultimaLezione(_, _, Settimana2, M), (Settimana2-Settimana1)>6.



% la prima lezione degli insegnamenti “Crossmedia: articolazione delle scritture multimediali” e 
% “Introduzione al social media management” devono essere collocate nella seconda settimana full-time
:- primaLezione(_, _,Settimana, crossmedia_articolazione_delle_scritture_multimediali), Settimana!=16.
:- primaLezione(_, _,Settimana, introduzione_al_social_media_management), Settimana!=16.

% la distanza fra l’ultima lezione di “Progettazione e sviluppo di applicazioni web su dispositivi mobile I” 
% e la prima di “Progettazione e sviluppo di applicazioni web su dispositivi mobile II” non deve superare 
% le due settimane.
:- primaLezione(_, _, Settimana2, progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_ii), ultimaLezione(_, _, Settimana1, progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i), (Settimana2-Settimana1)>2.

propedeuticita4(fondamenti_di_ict_e_Paradigmi_di_programmazione,progettazione_di_basi_di_dati;tecniche_e_strumenti_di_marketing_digitale,introduzione_al_social_media_management;comunicazione_pubblicitaria_e_comunicazione_pubblica,la_gestione_delle_risorse_umane;tecnologie_server_side_per_il_web,progettazione_e_sviluppo_di_applicazioni_web_su_dispositivi_mobile_i).

contOrePG(M1,Conteggio):- Conteggio == #count{ Ora:primoGiorno(Giorno,Ora,_,M1)}, primaLezione(Giorno,_,_,M1), propedeuticita4(M1,_).
:- contOrePG(M1,Conteggio), Conteggio<4.

% Qui avviene leffettiva eliminazione di tutti i modelli che non rispettano le propedeuticità delle 4 ore facendo iniziare la materia 
% di destra solo dopo il secondo giorno di lezione della materia sulla sinistra, in quanto essendo minimo 2 ore al giorno,
% superando il secondo giorno di lezione è sicuro che siano passate almeno 4 ore dellCorso di sinistra come da vincolo.
:- propedeuticita4(M1,M2), primaLezione(_,_,Settimana1,M1), primaLezione(_, _, Settimana, M2), Settimana<=Settimana1.
:- propedeuticita4(M1,M2), secondoGiornoLezione(_, _, Settimana2, M1), primaLezione(_, _, Settimana, M2), Settimana2<=Settimana.

:- propedeuticita4(M1,M2), secondoGiornoLezione(_, _, Settimana1, M1), primaLezione(_, _, Settimana2, M2), Settimana2<Settimana1.
:- propedeuticita4(M1,M2), secondoGiornoLezione(Giorno1, _, Settimana, M1), primaLezione(Giorno2, _, Settimana, M2),  Giorno2<=Giorno1.


lezionetoExcel(Settimana,Giorno,Inizio, Fine, Docente,Corso):- lezione(Giorno, Inizio, Fine, Corso, Settimana, Docente).


#show lezionetoExcel/6.
